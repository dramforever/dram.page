<!DOCTYPE html>
<html>

<head>
    <title>表达式归一化与 Free Monad -- dramforever</title>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link rel="stylesheet" href="../../styles/default.css">
</head>

<body>

<header id="site-title">
    <h1 id="site-title-main"><a href="/">
        dramforever
    </a></h1>
    <div id="site-title-sub">a row of my life</div>
</header>

<article>

<header id="post-title">
    <h1 id="post-title-main">表达式归一化与 Free Monad</h1>
    <span id="post-title-sub">2016-05-22</span>
</header>

<p>（注：正文部分代码里的下划线是链接，指向 Haddocks）</p>

<section>
<header>
  <h1>各种定义</h1>
</header>

<ul>
  <li>
    <code>Expression</code>:完整表达式，如 <code>f[u][u, foo[baz], q]</code>。具体定义如下：
    <ul>
      <li><code>Atom</code>: 一个常量符号是一个表达式，如 <code>foo</code></li>
      <li><code>Cons</code>: 一个函数调用是一个表达式，如 <code>?[?, ?, ...]</code>，其中问号代表完整表达式</li>
    </ul>
  </li>
  <li>
    <code>PartialExpr</code>: 在归一化的过程中，我们可能会遇到的情况是表达式的一个部分被指定为一个变量（如<code>#A</code>），这样我们得到的表达式如 <code>f[#A, u[#B], #C]</code>。在我们的实现中，变量名不由用户直接指定，这点上与 miniKanren 类似
  </li>
  <li>
    在数据结构的储存上，每个变量作为一个并查集节点，储存以下内容中的一个：
    <ul>
      <li>该变量没有任何限制</li>
      <li><code>Parent Var</code>: 该变量等于另一个变量</li>
      <li><code>Linked UnionFindExpr</code>: 该变量等于一个 <code>PartialExpr</code>。实现时的逻辑限制如果这个表达式是 <code>Cons</code>，则它必须全由变量组成，即形如 <code>#F[#A, #B, ...]</code></li>
    </ul>
  </li>
</ul>

<p>我们的目标是支持以下操作：</p>

<ul>
  <li><code>fresh</code>: 获得一个新的变量</li>
  <li><code>record</code>: 记录一个表达式，获得代表它的变量</li>
  <li><code>unify</code>: 指定两个变量相等</li>
  <li><code>report</code>: 获得当前一个变量的已知最精确表示</li>
</ul>

<p>如通过 <code>fresh</code> 获得变量，构造两个表达式 <code>f[#A, u[#B], #C]</code> 和 <code>#D[#E, #F, #G[v]]</code> 并 <code>record</code>，之后 <code>unify</code> 两个 <code>record</code> 得到的变量，再 <code>report</code> 其中一个，应该得到 <code>f[#E,u[#B],#G[v]]</code>。</p>
</section>

<section>
<header>
  <h1>表达式的代码实现</h1>
</header>

<p>我昨天晚上想到，其实三个表达式可以用相同的一个结构实现</p>

<pre><code>data ExprF a
  = Atom Identifier
  | Cons a [a]

type Expression = <a href="http://hackage.haskell.org/package/recursion-schemes-4.1.2/docs/Data-Functor-Foldable.html#t:Fix">Fix</a> ExprF
type PartialExpr = <a href="http://hackage.haskell.org/package/free-4.12.4/docs/Control-Monad-Free.html#t:Free">Free</a> ExprF Var
type UnionFindExpr = ExprF Var</code></pre>

<p>第一个并没有再次出现了，我们不管它。大家可以体会一下 <code>PartialExpr</code>。以下 sugar 可能有帮助：</p>

<pre><code>atom :: Identifier -> PartialExpr
atom = Free . Atom

cons :: PartialExpr -> [PartialExpr] -> PartialExpr
cons u v = Free (Cons u v)

instance IsString PartialExpr where
  fromString = atom . fromString</code></pre>
</section>

<section>
<header>
  <h1>信息维护 Monad 的实现</h1>
</header>

<pre><code>data UnionFindState
type MonadUFS m = MonadState UnionFindState m

fresh :: MonadUFS m => m Var

find :: MonadUFS m => Var -> m (Var, Maybe UnionFindExpr)

data UnificationError
  = AtomMismatch Identifier Identifier
  | AtomNotCons Identifier Var [Var]
  | ConsLengthMismatch UnionFindExpr UnionFindExpr

unify :: (MonadError UnificationError m, MonadUFS m)
      => Var -> Var -> m ()</code></pre>

<p>具体内容略去，可以参见结尾完整代码。这里多了一个内部使用的函数 <code>find</code>，其作用是找到与给定变量相等的一个变量，而这个变量具有性质：它不是被指定等于另一个变量，也就是顺着 <code>Parent</code> 走到顶端。另外，它还返回这个变量被指定等于的 <code>UnionFindExpr</code>（如果有的话）。</p>
</section>

<section>
<header>
  <h1><code>record</code> 和 <code>report</code> 的实现</h1>
</header>

<pre><code>record :: MonadUFS m => PartialExpr -> m Var
record = <a href="http://hackage.haskell.org/package/free-4.12.4/docs/Control-Monad-Free.html#v:iterA">iterA</a> go where
  go f = do
    u <- sequence f
    v <- fresh
    ufsMap . at v .= Just (Linked u)
    pure v</code></pre>

其中:
<pre><code>iterA :: (Applicative p, Functor f)
      => (f (p a) -> p a) -> Free f a -> p a</code></pre>
在这里使用的是
<pre><code>iterA :: MonadUFS m
      => (ExprF (m Var) -> m Var)
      -> PartialExpr
      -> m Var</code></pre>
<p>这里 <code>ufsMap . at v .= Just (...)</code> 是 lens 用法，意义是设定维护信息中变量 v 的指定。

<p>我们注意到，只要给 <code>ExprF</code> 定义时 <code>deriving (<a href="http://hackage.haskell.org/package/base-4.8.1.0/docs/Data-Traversable.html#t:Traversable">Traversable</a>)</code>：</p>
<pre><code>sequence :: MonadUFS m => ExprF (m Var) -> m UnionFindExpr</code></pre>

<p>也就是说，将传入参数 sequence 后得到的正好是我们需要创建的变量所指定相等的值！所以，我们成功地在不依赖于 <code>ExprF</code> 具体定义的情况下实现了 <code>record</code>！</p>

<p><code>report</code> 也非常简单：</p>

<pre><code>report :: MonadUFS m => Var -> m PartialExpr
report = <a href="http://hackage.haskell.org/package/free-4.12.4/docs/Control-Monad-Free.html#v:unfoldM">unfoldM</a> go where
  go v = find v >>= \case
    (u, Nothing) -> pure (Left u)
    (_, Just t) -> pure (Right t)</code></pre>

<p>至此，一个简易的表达式归一化程序就完成了。</p>
</section>

<section>
<header>
  <h1>总结</h1>
</header>

<p>在这个归一化程序中，我们很明显地发现，Free monad 的使用简化了表达式的定义和一些琐碎的功能的实现，有效避免了乏味而易错的代码重复。</p>
</section>

<section>
<header>
  <h1>附：完整实现代码</h1>
</header>

<style>
  #boring-code { display: none }
  input:checked + label + #boring-code { display: block }
</style>

<input type="checkbox" name="show-code">
<label for="show-code">展开代码</label>

<div id="boring-code">
  <pre><code>{-# LANGUAGE ConstraintKinds #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE DeriveTraversable #-}
{-# LANGUAGE TypeSynonymInstances #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE GeneralizedNewtypeDeriving #-}
{-# LANGUAGE MultiParamTypeClasses #-}
module Play where

import qualified Data.Text as T
import Control.Monad.Free
import qualified Data.Map as M
import Control.Lens hiding (cons)
import Control.Monad.State
import Control.Monad.Except
import Data.List (intercalate)
import Data.String
import Control.Applicative
import Data.Functor.Foldable (Fix(..))

newtype Identifier
  = Identifier { getIdentifier :: T.Text }
  deriving (IsString)

instance Show Identifier where
  show (Identifier u) = T.unpack u

data ExprF a
  = Atom Identifier
  | Cons a [a]
  deriving (Functor, Foldable, Traversable)

type Expression = Fix ExprF
type PartialExpr = Free ExprF Var
type UnionFindExpr = ExprF Var

printPartial :: PartialExpr -> String
printPartial = iter go . fmap show where
  go (Atom d) = show d
  go (Cons x xs) = x ++ "[" ++ intercalate "," xs ++ "]"

instance Show u => Show (ExprF u) where
  show (Atom (Identifier u)) = T.unpack u
  show (Cons x xs) = show x ++ "[" ++ intercalate "," (show <$> xs) ++ "]"

newtype Var
  = Var { getVar :: T.Text }
  deriving (Eq, Ord)

instance Show Var where
  show (Var v) = "#" ++ T.unpack v

data UnionFindPointer
  = Parent Var
  | Linked UnionFindExpr

$(makePrisms ''UnionFindPointer)

data UnionFindState
  = UnionFindState
    { _ufsSupply :: [Var]
    , _ufsMap :: M.Map Var UnionFindPointer
    }

$(makeLenses ''UnionFindState)

type MonadUFS m = MonadState UnionFindState m

fresh :: MonadUFS m => m Var
fresh = do
  ufsSupply %= tail
  use (ufsSupply . to head)

find :: MonadUFS m => Var -> m (Var, Maybe UnionFindExpr)
find u = locateRoot u >>= \(x, e) -> (x, e) <$ compressPath u x where
  locateRoot t =
    use (ufsMap . at t) >>= \case
      Just (Parent v) -> locateRoot v
      Just (Linked ex) -> pure (t, Just ex)
      Nothing -> pure (t, Nothing)
  compressPath t x = go t where
    go m
      | m == x = pure ()
      | otherwise = use (ufsMap . at m) >>= \case
          Just (Parent par) -> do
            ufsMap . at m .= Just (Parent x)
            go par
          _ -> error "Internal error: find: Can't happen!"

data UnificationError
  = AtomMismatch Identifier Identifier
  | AtomNotCons Identifier Var [Var]
  | ConsLengthMismatch UnionFindExpr UnionFindExpr
  deriving (Show)

unify :: (MonadError UnificationError m, MonadUFS m) => Var -> Var -> m ()
unify u1 v1 = do
  (xu, eu) <- find u1
  (xv, ev) <- find v1
  when (xu /= xv) $ case (eu, ev) of
    (Nothing, _) -> ufsMap . at xu .= Just (Parent xv)
    (Just _, Nothing) -> ufsMap . at xv .= Just (Parent xu)
    (Just p, Just q) -> go p q where
      go (Atom x) (Atom y)
        | getIdentifier x == getIdentifier y = pure ()
        | otherwise = throwError (AtomMismatch x y)
      go (Atom x) (Cons y ys) = throwError (AtomNotCons x y ys)
      go m@Cons{} n@Atom{} = go n m
      go m@(Cons x xs) n@(Cons y ys)
        | length xs == length ys = do
          unify x y
          zipWithM_ unify xs ys
          ufsMap . at xu .= Just (Parent xv)
        | otherwise = throwError (ConsLengthMismatch m n)

record :: MonadUFS m => PartialExpr -> m Var
record = iterA go where
  go f = do
    u <- sequence f
    v <- fresh
    ufsMap . at v .= Just (Linked u)
    pure v

report :: MonadUFS m => Var -> m PartialExpr
report = unfoldM go where
  go v = find v >>= \case
    (u, Nothing) -> pure (Left u)
    (_, Just t) -> pure (Right t)

run :: StateT UnionFindState (Except UnificationError) a
    -> Either UnificationError a
run s = runExcept (evalStateT s (UnionFindState vars M.empty)) where
  vars =
    let u = "" : liftA2 (flip (:)) u ['A'..'Z']
    in map (Var . T.pack) u

atom :: Identifier -> PartialExpr
atom = Free . Atom

cons :: PartialExpr -> [PartialExpr] -> PartialExpr
cons u v = Free (Cons u v)

instance IsString PartialExpr where
  fromString = atom . fromString</code></pre>

  <p>以下是一个简单的测试</p>

  <pre><code>a1, a2 :: MonadUFS m => m PartialExpr

-- f[#A, u[#B], #C]
a1 = do
  a <- fresh
  b <- fresh
  c <- fresh
  pure $ cons "f" [pure a, cons "u" [pure b], pure c]

-- #D[#E, #F, #G[v]]
a2 = do
  d <- fresh
  e <- fresh
  f <- fresh
  g <- fresh
  pure $ cons (pure d) [pure e, pure f, cons (pure g) ["v"]]

test :: (MonadError UnificationError m, MonadUFS m) => m PartialExpr
test = do
  e1 <- a1
  e2 <- a2
  x <- record e1
  y <- record e2
  unify x y
  report x

-- ghci> let Right u = run (printPartial <$> test) in putStrLn u
-- f[#E,u[#B],#G[v]]
--
-- Note: The variable names are generated automatically. As you can see
-- the definitions of a1 and a2 do not specify names.</code></pre>
</div>
</section>
</article>
</body>
</html>
